apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'
apply plugin: 'distribution'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jacoco {
    toolVersion = "0.7.6.201602180812"
}

test {
    finalizedBy jacocoTestReport
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    jmh
}

sourceSets {
    jmh {
        java.srcDir 'src/jmh/java'
        resources.srcDir 'src/jmh/resources'
        compileClasspath += configurations.jmh + configurations.compile
        runtimeClasspath += configurations.jmh + configurations.runtime
    }
}

dependencies {
    compile 'com.offbytwo:docopt:0.6.0.20150202'
    compile 'org.roaringbitmap:RoaringBitmap:0.6.14'
    testCompile 'junit:junit:4.12'
    jmh project
    jmh 'org.openjdk.jmh:jmh-parent:1.12'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.12'
    jmh 'org.openjdk.jmh:jmh-generator-bytecode:1.12'
}

task benchmarksJar(type: Jar) {
    baseName = "benchmarks"
    from sourceSets.jmh.output
    from sourceSets.main.output
    from { configurations.jmh.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Main-Class': 'util.graph.GraphBench'
    }
}

mainClassName = "Pprcd"
applicationDefaultJvmArgs = ["-Dgreeting.language=en"]
archivesBaseName = 'pprcd'
applicationName = 'pprcd'